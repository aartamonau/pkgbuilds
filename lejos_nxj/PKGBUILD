# Contributor: Josh Rickmar <joshua.rickmar@gmail.com>
# Contributor: Joshua Gonzales <jgonzales6@ucmerced.edu>
# Maintainer: Aliaksiej Artamona≈≠ <aliaksiej.artamonau@gmail.com>

pkgname=lejos_nxj
pkgver=0.8.5
_pkgver=0_8_5beta
pkgrel=2
pkgdesc="Tools for programming the Lego NXT brick in Java"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'APACHE')
url="http://lejos.sourceforge.net/"
depends=('java-environment' 'java-runtime'
         'bluez' 'libusb'
         'java-bluecove' 'java-commons-cli' 'java-bcel')
makedepends=('apache-ant')
source=(http://downloads.sourceforge.net/sourceforge/lejos/lejos_NXJ_${_pkgver}.tar.gz
        lejos_nxj_vars
        70-lego.rules
        nxj
        nxjbrowse
        nxjc
        nxjconsole
        nxjconsoleviewer
        nxjcontrol
        nxjdataviewer
        nxjflash
        nxjflashg
        nxjlink
        nxjmonitor
        nxjpc
        nxjpcc
        nxjsocketproxy
        nxjupload)

md5sums=('459907391c98edd3932ce33a59caf800'
         '108b13aa7b55957c29165e59c7d07297'
         '7d5aa5e40f00ff441c83c8377c471dba'
         'b18a05370a34f9a862d9ec795240c211'
         '35bd1b3c1b28b399ab52b5845f099dce'
         '558c1ce885d425580c2654b01cde8164'
         '8a92ae12723732bc0cd1ad801215fa85'
         '6edbc892f389d04b896a32c2296df0ba'
         'e6a92ab554d34ed2a01cc80c9db66724'
         'bce3c65001201946d8c064249cd6019e'
         '0610e0a98ada2ac6e9aa2f597fe1fee5'
         'ac363d29a86d9d19aa95b203b7958771'
         '98d3c94f841edce57f966cc8c6cd6f25'
         '9df88583c4b810307567a97fd34a6e99'
         '958fb6f6d921772309a7df6c6c317dd6'
         'dd1a1d6438c37ea6a3cd1468b6dbc7ea'
         'dc3f238c2d6ac6203e0185c6e3bc0acc'
         '9361c981ffe926b19eef24a9cea1209c')

install=${pkgname}.install

build() {
    local ANT_BIN="/usr/share/java/apache-ant/bin"
    local ANT=${ANT_BIN}/ant

    cd ${startdir}/src/lejos_nxj/build
    ${ANT} || return 1
    cd ..

    install -d -m755 ${pkgdir}/usr/share/java/lejos_nxj || return 1
    install -m644 -t ${pkgdir}/usr/share/java/lejos_nxj \
                     lib/*.jar || return 1

    install -d -m755 ${pkgdir}/usr/share/lejos_nxj/bin || return 1
    install -m644 -t ${pkgdir}/usr/share/lejos_nxj/bin \
                     bin/*.bin || return 1
    install -m644 -t ${pkgdir}/usr/share/lejos_nxj \
                     ${srcdir}/lejos_nxj_vars || return 1

    install -d -m755 ${pkgdir}/usr/lib || return 1
    install -m755 -t ${pkgdir}/usr/lib bin/*.so || return 1

    install -d -m755 ${pkgdir}/usr/bin || return 1
    cd ${srcdir}
    install -m 755 -t ${pkgdir}/usr/bin \
                      ${source[*]:3} || return 1

    install -d -m755 ${pkgdir}/etc/udev/rules.d || return 1
    install -m644 -t ${pkgdir}/etc/udev/rules.d \
                     ${srcdir}/70-lego.rules || return 1
}
